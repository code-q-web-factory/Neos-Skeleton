prototype(CodeQ.Site:Presentation.Organism.Section) < prototype(Neos.Fusion:Component) {
    @styleguide {
        props {
            content = Sitegeist.Monocle:Preview.Prototype {
                prototypeName = 'CodeQ.Site:Presentation.Atom.RichText'
            }
        }
    }

    @propTypes {
        @strict = true
        id = ${PropTypes.string}
        class = ${PropTypes.string}
        content = ${PropTypes.string}
        backgroundColor = ${PropTypes.oneOf(['white', 'light-gray'])}
        paddingY = ${PropTypes.oneOf(['none', 'medium'])}
        spaceBelow = ${PropTypes.oneOf(['none', 'small', 'medium', 'big'])}
    }

    id = null
    class = null
    content = null
    backgroundColor = 'white'
    paddingY = 'medium'
    spaceBelow = 'medium'

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        customClasses = Neos.Fusion:Join {
            base = '_section w-full relative'
            custom = ${props.class}
            paddingY = Neos.Fusion:Match {
                @subject = ${props.paddingY}
                'none' = ''
                'medium' = 'py-8 md:py-16'
            }
            backgroundColorClass = Neos.Fusion:Match {
                @subject = ${props.backgroundColor}
                'white' = 'bg-white'
                'light-gray' = 'bg-gray-50'
            }
            spaceBelowClass = CodeQ.Site:Presentation.Helper.SpaceBelow {
                @subject = ${props.spaceBelow}
            }
            @glue = ' '
        }
        renderer = afx`
            <section id={props.id} class={props.customClasses}>
                {props.content}
            </section>
        `
    }
}
