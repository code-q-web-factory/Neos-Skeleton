prototype(CodeQ.Site:Component.Header) < prototype(Neos.Fusion:Component) {
    homeLink = Neos.Neos:NodeUri {
        node = ${site}
    }
    menuItems = Neos.Neos:MenuItems {
        startingPoint = ${site}
        maximumLevels = 2
    }

    ##
    # If you want to use a default rendering for a menu, you can just use:
    ##
    # <Neos.Neos:Menu
    #     startingPoint={site}
    #     attributes.class="navigation"
    #     normal.attributes.class="navigation-item"
    #     active.attributes.class="navigation-item navigation-item--state-active"
    #     current.attributes.class="navigation-item navigation-item--state-current"
    #     />
    ##

    renderer = afx`
        <header class="main-header">
            <a class="logo" href={props.homeLink}>BRAND NAME</a>
            <CodeQ.Site:Component.Header.MenuItemsRenderer items={props.menuItems} />
        </header>
    `

	@cache {
		mode = 'cached'
		entryIdentifier {
			documentNode = ${documentNode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
		}
	}
}
