prototype(CodeQ.Site:Component.Footer) < prototype(Neos.Fusion:Component) {
    year = ${Date.format(Date.now(), 'Y')}
    footerInfo = Neos.Neos:Editable {
        node = ${site}
        property = 'footerInfo'
    }
    footerItems = ${q(site).property('footerItems')}

    renderer = afx`
        <footer class="main-footer">
            <div>{props.footerInfo}</div>
            <div>&copy; {props.year}</div>
            <ul class="main-footer__menu">
                <Neos.Fusion:Loop items={props.footerItems} itemName="menuItem">
                    <li class="main-footer__menu-item">
                        <Carbon.Link:Link node={menuItem} additionalClass="main-footer__menu-link" />
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </footer>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            site = ${site}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(site)}
        }
    }

    // The footer should only be inline editable on the home page,
    // otherwise the Neos UI will show a weird behavior
    @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
        @if.isOnSiteNode = ${site == documentNode}
        node = ${site}
        @position = 'end'
    }
}
