prototype(CodeQ.Site:Component.Footer) < prototype(Neos.Neos:ContentComponent) {
    @context.node = ${site}
    year = ${Date.format(Date.now(), 'Y')}
    footerInfo = Neos.Neos:Editable {
        node = ${site}
        property = 'footerInfo'
    }
    footerItems = ${q(site).property('footerItems')}

    renderer = afx`
        <footer class="main-footer">
            <div>{props.footerInfo}</div>
            <div>&copy; {props.year}</div>
            <ul class="main-footer__menu">
                <Neos.Fusion:Loop items={props.footerItems} itemName="menuItem">
                    <li class="main-footer__menu-item">
                        <Carbon.Link:Link node={menuItem} additionalClass="main-footer__menu-link" />
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </footer>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            site = ${site}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(site)}
        }
    }
}
